buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
	}
	repositories {
		mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'
apply plugin: "com.moowork.node"

jar {
	baseName = 'continuum'
	version = '0.0.1-SNAPSHOT'
}

def webProjectDir = "$project.projectDir/build/_webProject"
def webProjectOutputDir = "$project.projectDir/build/_webProject/dist"
def webProjectSvnUrl = 'https://vcs.mc.vanderbilt.edu/health-it/continuum/web/trunk'

import org.vumc.continuum.SvnCheckout

task checkoutWebProject(type: SvnCheckout) {
    svnUrl = webProjectSvnUrl
    projectDir = webProjectDir
}

node {
	download = false
	nodeModulesDir = file(webProjectDir)
}

npmInstall {
	dependsOn checkoutWebProject
}

npm_run_build {
	dependsOn npmInstall
}

war {
	// Download and build ContinuumWeb, removing "http://localhost:8080" from urls
	dependsOn npm_run_build
	from (webProjectOutputDir) {
		filter{ line -> line
				.replaceAll(/http:\/\/localhost:\d+\//,'/continuum/')
				.replaceAll('src="/','src="/continuum/')
				.replaceAll('href="/','href="/continuum/')
				.replaceAll(/\/(continuum\/){2,}/,'/continuum/')}
	}
    // Spring Boot IDEA plugin doesn't recognize "provided", exclude these jars manually instead
    classpath = classpath.filter {f -> !f.name.toLowerCase().find(/undertow|tomcat|jetty|h2|ojdbc/)}
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
    maven {
        url 'http://maven.jahia.org/maven2/'
    }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.springframework.boot:spring-boot-starter-integration')
	compile('org.springframework.boot:spring-boot-starter-websocket')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')

	// Reactive Streams
	compile('io.reactivex:rxjava:1.1.6')

    // HL7
    compile 'ca.uhn.hapi:hapi-base:2.2'
    compile 'ca.uhn.hapi:hapi-structures-v22:2.2'
    compile 'ca.uhn.hapi:hapi-structures-v25:2.2'

	// Convenience libraries
	compile("com.google.guava:guava:21.0")
    compile "org.functionaljava:functionaljava:4.6"
	compile "org.functionaljava:functionaljava-java8:4.6"
	compile "org.functionaljava:functionaljava-quickcheck:4.6"
	compile "org.functionaljava:functionaljava-java-core:4.6"

    // Auto-pluralizes object names.
    runtime 'org.atteo:evo-inflector:1.2.2'

    // Needed to support new Java 8 date types.
    // In JSON...
	runtime "com.fasterxml.jackson.module:jackson-module-parameter-names"
    runtime "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
    runtime "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.6"

    // ...And in Hibernate too.
    runtime "org.hibernate:hibernate-java8:5.0.11.Final"

    //compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    //compile("org.bitbucket.b_c:jose4j:0.5.0")

    // Dev-only dependencies. These MUST be excluded in the war {} config above.
    runtime('org.springframework.boot:spring-boot-devtools')
    runtime("com.h2database:h2:1.4.194")
    runtime('com.oracle:ojdbc6:12.1.0.2')

    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR5"
	}
}
